go athena
#
# Set up a mesh suitable for a single MOSFET device....
#
line x loc=0.0 spac=0.1 
line x loc=0.1 spac=0.006
line x loc=0.21 spac=0.006
line x loc=0.3 spac=0.01 
#
line y loc=0.0 spac=0.002 
line y loc=0.063 spac=0.002
line y loc=0.1 spac=0.005
line y loc=0.6 spac=0.015 
#

#
# Start off by defining silicon with 1e14 phos doping...

init orientation=100 c.phos=1e13 space.mult=2
# 

diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#N-well Implant

implant amorphous phos dose=1.0e13 energy=100 pears
#
diffus temp=950 time=100 weto2 hcl=3
#

# welldrive 
diffus time=220 temp=1200 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
diffus time=4 temp=900 dryo2 press=1.00 hcl=3
#
# Extract a design parameter.....
extract name="gateox" thickness oxide mat.occno=1 x.val=0.05
#
#vt adjust implant amorphous 
implant phos dose=5.0e11 energy=8.0 pearson

depo poly thick=0.1 divi=10 
#


#from now on the situation is 2-D
#
etch poly left p1.x=0.21
#
method fermi compress
diffuse time=1 temp=900 weto2 press=1.0
#
# PLDD implant amorphous
implant amorphous bf2 dose=1.0e13 energy=10 pearson
#
# spacer
depo oxide thick=0.030 divisions=8
#
etch oxide dry thick=0.030
#
# P++ Implant
implant bf2 dose=5.0e15 energy=12 pearson
#
#
# Final anneal. 
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#

# pattern s/d contact metal
etch oxide left p1.x=0.14
deposit alumin thick=0.015 divi=2
etch alumin right p1.x=0.12





# mirror the structure.....
structure mirror right

# extract the surface conc under the channel....
extract name="pchan surf conc" surf.conc impurity="Net Doping" material="Silicon" mat.occno=1 x.val=0.45
# extract final S/D Xj...
extract name="pxj" xj silicon mat.occno=1 x.val=0.1 junc.occno=1
# Extract a design parameter.....
extract name="gateox" thickness oxide mat.occno=1 x.val=0.005



# Name the electrodes...
electrode name=gate x=0.3 y=0.1
electrode name=source x=0.1
electrode name=drain x=0.5
electrode name=substrate backside

# output the structure
structure outfile=pmos_0.str 

# plot it
#tonyplot  pmos_0.str 

############# PVt Test : Returns PVt, PBeta and PTheta ################
go atlas

# set material models
models cvt srh print  
contact name=gate p.poly
interface qf=3e10

method  newton 
solve init

# Bias the drain a bit...
solve vdrain=-0.1

# Ramp the gate
log outf=pmos_1.log master
solve vgate=0 vstep=-0.1 vfinal=-3.0 vdrain=-0.1 name=gate
save outf=pmos_0.str






# extract device parameters......
tonyplot    pmos_1.log 
quit